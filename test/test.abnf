{+}
{}

//
$pata =  我 {+我}
service a {

}


#ABNF 1.0 UTF-8;
mode SMS_USE_SUB;
meta;

partial;

#include "..\Common\common.abnf";
#include "..\BasicPhrase\BP_declare.abnf";

root p_main;
#ABNF HEAD-END;




//业务名：流量短信
//功能：解析短信当中$SW_De关于流量$SW_De一些字段信息
//编写：JK
//时间：2015-8-6
//优化：JK

//支持x M x KB等类似说法
//数值金额变量
$Int1 = $SW_DigitHZ|$SW_Digit;
$Int2 = [$Int1] 十 [$Int1]|$BP_DS2;
$Int2_ = $Int2|$Int1;
$Int3 = ($Int1|两)百 [$Int2|(零)$Int1]|$BP_DS3;
$Int4 = ($Int1|两)千 [$Int3|(零)$Int2_]|$BP_DS4;

$digit1 = $SW_Digit;
$digit2 = $SW_DigitHZ|十|零|两;
$digit_else = ($Int1|两|$Int2|$Int3|$Int4) 万 $Int4 ;//十六万八千

$amount_yuan =  ($BP_Number|$digit_else) (G|g|GB|gb);
$amount_jiao =  ($BP_Number|$digit_else) (M|m|兆|MB|mb);
$amount_fen =  ($BP_Number|$digit_else) (KB|kb|k|K);

$amount = $amount_yuan | $amount_jiao | $amount_fen | $amount_yuan [零] $amount_jiao [零] $amount_fen | $amount_yuan [零] $amount_jiao |$amount_yuan [零] $amount_fen | $amount_jiao [零] $amount_fen;
$digit = 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 | 一 | 二 | 三 | 四 | 五 | 六 | 七 | 八 | 九 | 十 | 两 | 俩 | 壹 | 贰| 叁 |肆|伍|陆| 柒| 捌| 玖 | 拾 | 百 | 千 | 万;
$liuliang = ($digit)<1-5>(G|g|M|m|兆|KB|kb|MB|mb|k|K|GB|gb) [整] [$SW_De] | $amount [$SW_De] | ($digit)<1-5>(.)($digit)<1-5>(G|g|M|m|兆|KB|kb|MB|mb|k|K|GB|gb) [整] [$SW_De];

//添加无单位
$amount1 = $digit_else|$BP_Integer2|$BP_Integer3|$BP_Integer4|$BP_Integer5|$BP_Integer6|$BP_Integer7|$BP_Integer8 ;//无单位$SW_De金额：买车票花了一百、十六
$amount2 = ($digit1|$digit2) (百|千|万) ($digit1|$digit2) ;//无单位$SW_De金额：一百六、一千六、一万六
$amount3 = $amount | $amount1 | $amount2 ;
$liuliang_无单位 =  ($digit)<1-5>[G|g|M|m|兆|KB|kb|MB|mb|k|K|GB|gb] [整] [$SW_De] | $amount [$SW_De]  | $amount3 [$SW_De] | ($digit)<1-5>(.)($digit)<1-2>[G|g|M|m|兆|KB|kb|MB|mb|k|K|GB|gb] [整] [$SW_De];


//核心片段
//已用流量
$usedflow = ([本月]已[使]用[$_ti_ch_<0-6>]流量|[本月](已产生|已[使]用|产生|使用|当月使用的GPRS) [$SW_De] 总流量|本月[手机上网]流量已使用)[MB|mb] [不含免费流量|含免费流量|$_ti_ch_<0-6>] [为|是|还有|有|还剩|还剩下] $liuliang{+usedFlow} | 已[使]用[移动数据流量[为]|$_ti_ch_<0-6>] $liuliang{+usedFlow} [$SW_De] 流量 | [本月](已产生|已[使]用|使用|产生) $liuliang{+usedFlow} [$SW_De] 总流量 | (使用流量(为|是)|本次上网流量[为|是]) $liuliang{+usedFlow} | (移动数据|套餐内)流量已[使]用 $liuliang{+usedFlow} | 当月总上网 [$SW_De] 流量 [为|是] $liuliang{+usedFlow} |本月使用总流量 \(含免费流量\) [为|是] $liuliang{+usedFlow} ;

//剩余流量
$leftflow = [套餐内] 剩余[$_ti_ch_<0-6>]流量 [为|是|还有|有|剩余|还剩|还剩下][MB|mb|$_ti_ch_<0-6>] $liuliang{+leftFlow} | 剩余|还剩[下] [$_ti_ch_<0-6>] $liuliang{+leftFlow} [$SW_De] [流量];

//总流量
$totalflow = (总流量|套餐内流量共|[本月]套餐包含数据流量|本月套餐及获赠流量|套餐内[含]移动数据总流量|WLAN高校资费免费流量总计|当前数据流量| 上网套餐总流量| 手机上网(包含|流量优惠可享受) | 手机流量|上网流量)[MB|mb] [含免费流量|$_ti_ch_<0-10>] [为|是|还有|[共]有|剩余|还剩|还剩下|共] $liuliang{+totalFlow};

//片段组合5
//上网流量(1024.00MB) --已用0.00MB --剩余1024.00MB
$片段1 = $totalflow [$_ti_ch_<0-2>] [(已[使]用|使用) [$_ti_ch_<0-2>] $liuliang{+usedFlow} [$SW_De] [流量]] [$_ti_ch_<0-4>] [现] ([还]剩余|还剩|还余)[$_ti_ch_<0-2>] $liuliang{+leftFlow} [$SW_De] [流量];
$片段2 = [优惠]已[使]用[$_ti_ch_<0-2>|移动数据流量[为|是]] $liuliang{+usedFlow} [$SW_De] [流量] [$_ti_ch_<0-2>] [优惠] [现] ([还]剩余|还剩|还余)[$_ti_ch_<0-2>] $liuliang{+leftFlow} [$SW_De] [流量];
$片段3 = ($totalflow|上网套餐总流量 [$liuliang_无单位]) [$_ti_ch_<0-10>|gb|GB] [实际] (已[使]用|使用) [$_ti_ch_<0-2>|流量] $liuliang{+usedFlow} [$SW_De] [流量];
//手机上网流量3062MB,截至12日16时49分,已经使用5791.84MB
$片段37 = 手机上网流量 $liuliang{+totalFlow} [截至$_ti_ch_<0-2>日$_ti_ch_<0-2>时$_ti_ch_<0-2>分|$_ti_ch_<0-10>] [实际] (已[经][使]用|使用) [$_ti_ch_<0-2>|流量] $liuliang{+usedFlow} [$SW_De] [流量];


//套餐外流量
$outusedflow = (套餐外[已使用[的]]流量|套餐外使用[了]|套餐外收费流量)[MB|mb|$_ti_ch_<0-5>] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+outUsedFlow};

//已使用夜间流量
$nightflowused = (已[使]用[$_ti_ch_<0-6>]夜间[$_ti_ch_<0-6>]流量|夜间[$_ti_ch_<0-6>]流量已[使]用|夜间[$_ti_ch_<0-6>]已[使]用流量|夜间流量包 | 闲时流量已[使]用)[MB|mb|$_ti_ch_<0-10>] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+nightFlowUsed} | 夜间已[使]用[$_ti_ch_<0-6>] $liuliang{+nightFlowUsed} [$SW_De] 流量;

//剩余夜间流量
$nightflowleft = (剩余夜间[$_ti_ch_<0-6>]流量|夜间流量剩余)[MB|mb|$_ti_ch_<0-6>] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+nightFlowLeft};

//夜间总流量
//闲时相关的全部解析到夜间 字段
$nightflowtotal = (夜间[$_ti_ch_<0-6>]总流量|夜间流量共包含数据流量|夜间流量包套餐包含流量 | 本地[$SW_De]闲时流量 | [夜间]闲时流量合计 | 省内闲时[手机上网]流量)[MB|mb] [为|是|还有|有|剩余|还剩|还剩下|共|余] $liuliang{+nightFlowTotal} | 夜间流量包本月内含 $liuliang{+nightFlowTotal} [$SW_De] [省内闲时移动数据]流量 ;

//片段组合1
$片段4 = $nightflowused [现][还]剩[余][$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+nightFlowLeft};
$片段5 = ($nightflowtotal | 省内夜间流量\(23点-7点有效\))[$_ti_ch_<0-2>] [已[使]用[$_ti_ch_<0-2>][流量][MB|mb] $liuliang{+nightFlowUsed}] [现] [还]剩[余][$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+nightFlowLeft};
//省内闲时手机上网流量1024.00MB，已使用43.78MB，目前剩余980.22MB
$片段21 = $nightflowtotal 已[使]用 $liuliang{+nightFlowUsed} [现|目前] [还]剩[余][$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+nightFlowLeft};

//===============省内流量片段==================
//能识别值
$省内_总_只有值 = [MB|mb] [ 可享受 | 合计 | (共[计]) | 为 | 是 | ([还]有) | 剩余 | (还剩[下]) ] $liuliang{+provinceFlowTotal};
$省内_剩余_只有值 = [MB|mb] [为 | 是 | ([还]有) | （[剩]余） | (还剩[下]) ] $liuliang{+provinceFlowLeft};
$省内_已用_只有值 = [MB|mb] [为 | 是 | ([还]有) | 剩余 | (还剩[下]) ] $liuliang{+provinceFlowUsed};
$省内_套外_只有值 = [MB|mb] [为 | 是 | ([还]有) | 剩余 | (还剩[下]) ] $liuliang{+provinceFlowOutUsed};

//能区分是总量、剩余、已用还是套外，不能识别是不是省内
$省内_总_有类型 = [总]流量 $省内_总_只有值;
$省内_剩余_有类型 = ((([剩]余[省内][手机|$_ti_ch_<0-6>]流量) | (流量[剩]余) ) $省内_剩余_只有值) | (剩余 $省内_剩余_只有值 [$SW_De] [流量]);
$省内_已用_有类型 = (((流量已[使]用) | (已[使]用流量) $省内_已用_只有值) $省内_已用_只有值) | (已使用 $省内_已用_只有值 [$SW_De] [流量]);
$省内_套外_有类型 = 套餐外使用流量 $省内_套外_只有值;

//能区分是省内
$省内_总_有关键字 = (省内| \[省内\] ) [手机|$_ti_ch_<0-6>] $省内_总_有类型;
$省内_剩余_有关键字1 = 剩余 省内 [手机|$_ti_ch_<0-6>] 流量 $省内_剩余_只有值;
$省内_剩余_有关键字2 = 省内|( \[省内\] ) [手机|$_ti_ch_<0-6>] $省内_剩余_有类型;
$省内_已用_有关键字1 = 已[使]用 省内 [手机|$_ti_ch_<0-6>]流量 $省内_已用_只有值;
$省内_已用_有关键字2 = 省内|( \[省内\] ) [手机|$_ti_ch_<0-6>] $省内_已用_有类型;
$省内_套外_有关键字 = 省内|( \[省内\] ) [手机|$_ti_ch_<0-6>] $省内_套外_有类型;

//省内流量(M):总30,余0.00
$省内_特殊片段_无单位 = 省内流量 [$_ti_ch_<0-4>] 总 $liuliang_无单位{+provinceFlowTotal} [目前|当前][还][剩]余 $liuliang_无单位{+provinceFlowLeft};

//起始的类型，一定要能区分省内
$省内_总_起始 = $省内_总_有关键字 [$省内_已用_有类型] [$省内_剩余_有类型];
$省内_剩余_起始 = $省内_剩余_有关键字1|$省内_剩余_有关键字2;
$省内_已用_起始 = $省内_已用_有关键字1|$省内_已用_有关键字2 [$省内_剩余_有类型] ;

$省内 = $省内_总_起始 | $省内_剩余_起始 | $省内_已用_起始 | $省内_套外_有关键字 | $省内_特殊片段_无单位;

$片段_省内易信 = 易信省内定向流量 [$_ti_ch_<0-2>] 已[使]用 $liuliang{+provinceFlowTotal} [现|目前|$_ti_ch_<0-2>] 还[剩]余[$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+provinceFlowLeft};
//===============省内流量片段-end==================


//国内已使用流量
$countryflowUsed = (国内[手机|$_ti_ch_<0-6>][通用]已[使]用流量|已[使]用国内[手机|$_ti_ch_<0-6>][通用]流量|国内[手机|$_ti_ch_<0-6>][通用]流量已[使]用 | 手机上网国内流量已[使]用)[MB|mb] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+countryFlowUsed} | 国内[手机|$_ti_ch_<0-6>]已[使]用 $liuliang{+countryFlowUsed} [$SW_De] 流量;

//国内剩余流量
$countryflowLeft = (国内[手机|$_ti_ch_<0-6>][通用]剩余流量|剩余国内[手机|$_ti_ch_<0-6>][通用]流量|国内[手机|$_ti_ch_<0-6>][通用]流量[剩]余 | 国内流量剩)[MB|mb] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+countryFlowLeft} | 国内[手机|$_ti_ch_<0-6>]剩余 $liuliang{+countryFlowLeft} [$SW_De] 流量;

//国内总流量
$countryflowTotal = (国内[手机|$_ti_ch_<0-6>][通用]总流量|国内手机上网流量|国内通用流量|国内[无线]上网流量 | 国内后向流量 | 国内[$_ti_ch_<0-10>]流量合计 | 手机上网国内[超出靠档]流量 | 国内漫游上网[$_ti_ch_<0-16>] | 国内[$_ti_ch_<0-6>]数据流量可享受 | 国内全时段流量包[$_ti_ch_<0-12>|和夜间流量包套餐包含流量] | 国内\(不含港澳台\)免费流量总计 | 国内\(含4G专用等\)流量合计|国内上网$_ti_ch_<0-8>\(不含WiFi\)$_ti_ch_<0-2>)[MB|mb] [为|是|还有|有|剩余|还剩|还剩下|共] [总量[为]|$_ti_ch_<0-10>] $liuliang{+countryFlowTotal};

//国内套餐外使用流量
$countryflowOutused = 国内[手机|$_ti_ch_<0-6>]套餐外使用流量[MB|mb] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+countryFlowOutUsed};

//片段组合3
//国内流量已用0.00MB，剩余100.00MB
$片段8 = $countryflowUsed [现|目前|$_ti_ch_<0-2>] [还]剩余[$_ti_ch_<0-6>][流量|量][MB|mb] $liuliang{+countryFlowLeft} ;
$片段9 = ($countryflowOutused|\[国内\]) 已[使]用[$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+countryFlowUsed} [现|目前|$_ti_ch_<0-2>] 剩余[$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+countryFlowLeft};
//国内上网流量30.00M,已使用30.00M
$片段10 = ($countryflowTotal|国内上网流量) [本月|$_ti_ch_<0-2>] [实际] (已[使]用|使用) [$_ti_ch_<0-2>][流量][MB|mb] $liuliang{+countryFlowUsed} [[现|目前|$_ti_ch_<0-2>] [还]剩[余][$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+countryFlowLeft}];
//国内上网流.量_共享(不含WiFi)的总量为100.00M，还剩余100.00M
$片段36 = $countryflowTotal [现|目前|$_ti_ch_<0-2>] [还]剩[余[量]][$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+countryFlowLeft};

//$片段30 = $countryflowTotal [本月|$_ti_ch_<0-2>] [实际] (已[使]用|使用) [$_ti_ch_<0-6>][流量][MB|mb] $liuliang{+countryFlowUsed};


//加油包已使用流量
$AddgprsUsed = ((加油包|gprs流量套包|流量套包|随意玩)已[使]用流量|(加油包|gprs流量套包|流量套包|随意玩)流量已[使]用|已[使]用(加油包|gprs流量套包|流量套包|随意玩)流量|定向流量已[使]用|专用流量已[使]用|充值流量已[使]用)[MB|mb] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+addGprsUsed} | (加油包|gprs流量套包|流量套包|随意玩)已[使]用[流量] $liuliang{+addGprsUsed} [[$SW_De] 流量];

//加油包剩余流量
$AddgprsLeft = ((加油包|gprs流量套包|流量套包|随意玩)剩余流量 | (加油包|gprs流量套包|流量套包|随意玩)流量剩余 | 剩余(加油包|gprs流量套包|流量套包|随意玩)流量 | 剩余gprs|剩余GPRS) [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+addGprsLeft};

//加油包总流量
$AddgprsTotal = (加油包|gprs流量套包|流量套包|随意玩|流量半年包|流量放心包|WLAN高校资费免费流量|wlan高校资费免费流量) (总流量|总计) [$_ti_ch_<0-2>] [MB|mb] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+addGprsTotal} | (每月含QQ流量|套餐包含流量|区内掌厅上网流量|定向流量合计) [总计] [MB|mb] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+addGprsTotal} | 放心包[$_ti_ch_<0-12> 元 $_ti_ch_<0-12> 元 $_ti_ch_<0-12>]总计 [$_ti_ch_<0-2>] [MB|mb] [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+addGprsTotal};

//片段组合4
$片段11 = $AddgprsUsed [现|$_ti_ch_<0-2>] ([还]剩余|还剩|还余)[$_ti_ch_<0-6>][流量] $liuliang{+addGprsLeft};
$片段12 = ($AddgprsTotal | 充值流量已[使]用 | 已[使]用充值流量) [$_ti_ch_<0-2>] (已[使]用|使用) [$_ti_ch_<0-6>][流量] $liuliang{+addGprsUsed} [目前|$_ti_ch_<0-2>] [现] ([还]剩余|还剩|还余)[$_ti_ch_<0-6>][流量] $liuliang{+addGprsLeft};

$片段23 = $AddgprsTotal [目前|$_ti_ch_<0-2>] [现|$_ti_ch_<0-2>] ([还]剩余|还剩|还余)[$_ti_ch_<0-6>][流量] $liuliang{+addGprsLeft};

//$片段19 = (充值流量已[使]用 | 已[使]用充值流量) [为|是|还有|有|剩余|还剩|还剩下] $liuliang{+addGprsUsed} [现|$_ti_ch_<0-2>] ([还]剩余|还剩|还余)[$_ti_ch_<0-6>][流量] $liuliang{+addGprsLeft};

//片段组合6
//$片段13 = 定向流量已[使]用[$_ti_ch_<0-2>] $liuliang ([还]剩余|还剩|还余)[$_ti_ch_<0-2>] $liuliang{+leftFlow} [$SW_De] [流量];

$片段14 = (省内手机上网闲时套餐|18元流量卡300m省内[闲时]|3g手机上网包本地[$_ti_ch_<0-12>]|神州行8元流量版赠送100m省内[闲时]|国内上网流量) 已[使]用[$_ti_ch_<0-2>] $liuliang{+usedFlow} [$SW_De] [流量] [$_ti_ch_<0-2>] ([还]剩余|还剩|还余)[$_ti_ch_<0-2>] $liuliang{+leftFlow} [$SW_De] [流量];

//当月剩余量为100MB,套餐内总流量为100MB， 使用流量为0MB
$片段15 = [当月] 剩余量 (为|是) $liuliang{+leftFlow} 套餐内总流量 (为|是) $liuliang{+totalFlow} 使用流量 (为|是) $liuliang{+usedFlow};

//本月已使用流量（不含免费流量） 34.39MB 本月产生的总流量（含免费流量） 34.39MB 套餐内流量 34.39MB 套餐内剩余流量 415.62MB
$片段16 = 本月已[使]用[$SW_De]流量 \(不含免费流量\)  [为|是] $liuliang{+usedFlow} 本月产生[$SW_De]总流量 \(含免费流量\)  [为|是] $liuliang 套餐内流量 [为|是] $liuliang 套餐内剩余流量 [为|是] $liuliang{+leftFlow};

//新增三个字段，用来解析本地流量
//本地已使用流量
$localFlowUsed = (本地[$SW_De|$_ti_ch_<0-6>]已[使]用流量|本地[$SW_De|$_ti_ch_<0-6>]流量已[使]用) [$SW_De] [为|是] $liuliang{localFlowUsed} | 本地[$SW_De|$_ti_ch_<0-6>](已[使]用|使用) $liuliang{localFlowUsed} [$SW_De] 流量 ;

//本地剩余流量
$localFlowLeft = (本地[$SW_De|$_ti_ch_<0-6>][还]剩[余] [$SW_De] 流量 | 本地[$SW_De|$_ti_ch_<0-6>]流量 [现] [还]剩[余]) [为|是] $liuliang{localFlowLeft} | 本地[$SW_De|$_ti_ch_<0-6>][还]剩[余] $liuliang{localFlowLeft} [$SW_De] 流量 ;

//本地总流量
$localFlowTotal = (本地 [$SW_De|$_ti_ch_<0-6>] 总流量 | 本地上网[$SW_De]流量 | 手机上网本地[$SW_De]流量) [为|是] $liuliang{localFlowTotal};

$片段17 = ($localFlowTotal|本地[上网][流量]) [$_ti_ch_<0-2>] (已[使]用|使用) $liuliang{localFlowUsed} [$SW_De] [流量] [$_ti_ch_<0-2>] [现] [[还]剩[余] $liuliang{localFlowLeft} [$SW_De] [流量]];

//$片段18 = 本地[流量](已[使]用|使用) $liuliang{localFlowUsed} [$SW_De] [流量] [$_ti_ch_<0-2>] [现] [还]剩[余] $liuliang{localFlowLeft} [$SW_De] [流量];

//手机上网包含60.00M，剩余21.94M
//$片段20 = 手机上网(包含|流量优惠可享受) $liuliang{+totalFlow} [$_ti_ch_<0-2>] [现][还]剩[余] $liuliang{+leftFlow} ;

//手机流量30MB已用0MB
//$片段22 = 手机流量 $liuliang{+totalFlow} [$_ti_ch_<0-2>] (已[使]用|使用) $liuliang{+usedFlow} ;


//添加无单位特殊情况
//免GPRS4G流量(M):总2048,余2045.36
$片段25 = 免gprs4g流量 [$_ti_ch_<0-4>] 总 $liuliang_无单位{+addGprsTotal} [目前|当前][还][剩]余 $liuliang_无单位{+addGprsLeft};

//移动数据流量(M):总5,余0.00
$片段26 = 移动数据流量 [$_ti_ch_<0-4>] 总 $liuliang_无单位{+totalFlow} [目前|当前][还][剩]余 $liuliang_无单位{+leftFlow};

//省内流量-流量加油包已使用407.50MB，剩余616.50MB；省内流量-流量加油包已使用0.00MB，剩余1024.00MB；省内流量-套餐内已使用200.00MB，剩余0.00MB；省内流量-流量成长包已使用200.00MB，剩余0.00MB
$片段28 = 省内流量 [$_ti_ch_<0-6>] [流量](加油包|成长包|套餐内|包) (已[使]用|使用) $liuliang{+provDetailFlowUsed} [还]剩[余]  $liuliang{+provDetailFlowLeft};

//国内上网流量：已使用270.13MB,剩余753.87MB易信省内定向流量：已使用0MB,剩余500MB
$片段31 = 国内上网流量 [$_ti_ch_<0-2>] (已[使]用|使用) $liuliang{+countryFlowUsed} [还]剩[余] $liuliang{+countryFlowLeft} $片段_省内易信 ;

//国内上网流量：已使用19.69MB,剩余1004.31MB优酷定向流量：已使用0MB,剩余1024MB
$片段32 = 国内上网流量 [$_ti_ch_<0-2>] (已[使]用|使用) $liuliang{+countryFlowUsed} [还]剩[余] $liuliang{+countryFlowLeft} 优酷定向流量 [$_ti_ch_<0-2>] (已[使]用|使用) $liuliang{+addGprsUsed} [还]剩[余] $liuliang{+addGprsLeft} ;

//当月可用流量300.00M，已使用25.67M
$片段33 = 当月可用流量 $liuliang{+totalFlow} 已[使]用 $liuliang{+usedFlow} ;

//数据流量套餐内应优惠流量共240.00M。截至查询时套餐内已累计使用流量10.29M,套餐外已使用流量0M
$片段34 = 数据流量套餐内应优惠流量共 $liuliang{+totalFlow} [$_ti_ch_<0-6>] 套餐内已累计使用流量 $liuliang{+usedFlow} [$_ti_ch_<0-6>] 套餐外已使用流量 $liuliang{+outUsedFlow};

//50元包800M剩263.58M
$片段35 = ($amount1 | $amount2) 元[加油|流量]包 $liuliang{+addGprsTotal} [还]剩[下|余] $liuliang{+addGprsLeft};

$p_main{service:flowparse} = $_ti_filler_ ($usedflow | $leftflow | $totalflow | $outusedflow | $nightflowused | $nightflowleft | $nightflowtotal | $countryflowUsed | $countryflowLeft | $countryflowTotal | $countryflowOutused | $AddgprsUsed | $AddgprsLeft | $AddgprsTotal | $片段1 | $片段2 | $片段3 | $片段4 | $片段5 | $省内 | $片段8 | $片段9 | $片段10 | $片段11 | $片段12 | $片段14 | $片段15 | $片段16 | $localFlowUsed | $localFlowLeft | $localFlowTotal | $片段17 | $片段21 |$片段23 | $片段25 | $片段26 | $片段28 | $片段31 | $片段32 | $片段33 | $片段34 | $片段35 | $片段36 | $片段37) $_ti_filler_;
